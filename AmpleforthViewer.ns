Newspeak3
'Root'
class AmpleforthViewer packageUsing: manifest = (
(*
The intent here was to create an app that would display an arbitrary Ampleforth document in a manner suitable for end-user consumption. The idea at the time was that we would use the HTML of the document coupled with the vfuel of this app, in a URL of the form: 
  https://DocName.html?snapshot=AmpleforthViewer.vfuel
However, this has several problems. The first is that it ends to display the document twice - once without live Newspeak widgets and once with them. The second is that non-standard classes and documents will be missing. The third is that the persistent format is now a zip file, not an HTML alone. So this needs rethinking. 
*)
	| 
    private HopscotchIDE = manifest HopscotchWebIDE packageUsing: manifest.
    |
) (
class Viewer usingPlatform: p = (
  |
  Presenter = p hopscotch Presenter.  
  Subject = p hopscotch Subject.
  Map = p collections Map.
  |
) (
class DocViewer onSubject: s = Presenter onSubject: s  (
) (
definition ^ <Fragment> = (
  ^ampleforth: subject text mapping: subject mapping
)
isMyKind: f <Fragment> ^ <Boolean> = (
  ^f isKindOfDocViewer
)
public isKindOfDocViewer ^ <Boolean> = (
  ^true
)
) : (
)
public class DocSubject onModel: m <Document> = Subject onModel: m (
  |
  public mapping = Map new at: #AmpleforthTargetDocument put: model; yourself.
  |
) (
public text ^ <String> = (
  ^model contents
)
isMyKind: s <Subject> ^ <Boolean> = (
  ^s isKindOfDocSubject
)
public isKindOfDocSubject ^ <Boolean> = (
  ^true
)
createPresenter ^ <DocViewer> = (
  ^DocViewer onSubject: self
)
) : (
)
) : (
)
public main: platform args: args = (
  | 
  ide <HopscotchWebIDE> = HopscotchIDE ideUsingPlatform: platform.
  doc <Document> = ide documents freshDocumentFromDOM: (platform js global at: 'document').
  window = platform hopscotch HopscotchWindow.
  viewer = Viewer usingPlatform: platform.
  docSubject = viewer DocSubject onModel: doc.
  |

  window openSubject: docSubject.  
)
) : (
)
